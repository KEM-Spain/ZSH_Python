#!/usr/bin/zsh
# Inline ansi
BOLD="\033[1m"
ITALIC="\033[3m"
RESET="\033[m"
REVERSE="\033[7m"
STRIKE="\033[9m"
UNDER="\033[4m"
BLACK_BG="\033[40m"
BLUE_FG="\033[34m"
CYAN_FG="\033[36m"
GREEN_FG="\033[32m"
MAGENTA_FG="\033[35m"
RED_FG="\033[31m"
WHITE_FG="\033[37m"
YELLOW_FG="\033[33m"
WHITE_ON_GREY="\033[0m\033[0;1;37;100m"

# Declarations
typeset -a _FILE_LIST=()
typeset -A _EDITS=()
typeset -A _EDIT_COORDS=()
typeset -A _FLD_ORDER=(FILE 1 ARTIST 2 TITLE 3 ALBUM 4)

# Imports
[[ -z ${ZSH_LIB_DIR} ]] && _LIB_DIR=/usr/local/lib || _LIB_DIR=${ZSH_LIB_DIR}
source ${_LIB_DIR}/LIB_INIT.zsh # Must be first
source ${_LIB_DIR}/UTILS.zsh
source ${_LIB_DIR}/SELECT.zsh
source ${_LIB_DIR}/LIB_DEPS.zsh # Must be last

# Vars
_GOT_DATA=false
_MODIFIED=false

# Functions
dep_exists () {
	local -a DEPS=(${@})
	local D

	for D in ${DEPS};do
		which ${D} >/dev/null 2>&1
		[[ ${?} -ne 0 ]] && echo ${D} && return 1
	done
	return 0
}

edit_field () {
	local LABEL=${1}
	local VALUE=${2}
	local FX=${3}
	local FY=${4}
	local NEW_VALUE

	{
	tput cup ${FX} ${FY}
	tput el
	cursor_on
	NEW_VALUE=$(inline_edit "${LABEL}:" ${VALUE}) # Launch line editor
	cursor_off
	} >&2

	echo ${NEW_VALUE}
}

inline_edit () {
	local PROMPT=${1}
	local CUR_VALUE=${2}
	local PERL_SCRIPT

	read -r -d '' PERL_SCRIPT <<'___EOF'
	use warnings;
	use strict;
	use Term::ReadLine;

	my $term = new Term::ReadLine 'data';
	$term->parse_and_bind("set editing-mode vi");

	system('sleep .1;xdotool key Home Escape space Left &'); # Trick to set the cursor to beginning in non destructive cursor mode
	while ( defined ($_ = $term->readline($ARGV[0],$ARGV[1])) ) {
		print $_;
		exit;
	}
___EOF

perl -e "$PERL_SCRIPT" ${PROMPT} ${CUR_VALUE}
}

tags_display () {
	local -A TAG_DATA=()
	local -a FIELDS=()
	local -a LABELS=()
	local BOX_X=14
	local BOX_Y=50
	local DATA
	local FN_2_TITLE=''
	local LBL
	local MUSIC_DIR="/media/kmiller/KEM_Misc/Music/KEM-B9"
	local NDX
	local NEW_VAL
	local VAL
	local F K X Y

	for F in ${(on)_FILE_LIST};do
		case ${F:e} in
			flac) tags_flac ${F};;
			mp3) tags_mp3 ${F};;
			wav) tags_mp3 ${F};;
		esac
		
		TAG_DATA=(${(kv)_EDITS})
		if arr_is_populated "${TAG_DATA}";then
			_GOT_DATA=true

			for K in ${(k)TAG_DATA};do
				DATA=${TAG_DATA[${K}]}
				if [[ ${K} == "TITLE" ]];then
					DATA=$(str_proper ${DATA}) # Proper case TITLE
				fi
				FIELDS[${_FLD_ORDER[${K}]}]="${K}:${DATA}"
				if [[ ${K} == "TITLE" && ${TAG_DATA[${K}]} == 'None' ]];then
					FN_2_TITLE=${F:t:r}
					FN_2_TITLE=$(perl -pe "s/^(\d+\s?[-]?)(.*)/\2/g" <<<${FN_2_TITLE})
					FIELDS[${_FLD_ORDER[${K}]}]="${K}:${FN_2_TITLE}" # Swap file name for missing TITLE
				fi
			done

			LABELS=("${(f)$(msg_list_data ${FIELDS})}")

			tcup 10 50
			echo -n "${WHITE_FG}Displaying tags for${RESET}:${CYAN_FG}${${PWD}##${MUSIC_DIR}}${RESET}"
			tcup 12 50
			echo -n "Enter '${WHITE_FG}e${RESET}' to edit, '${WHITE_FG}q${RESET}' to quit, <${WHITE_FG}ENTER${RESET}> for next track"

			msg_box -q -c -p -x${BOX_X} -y${BOX_Y} -w 60 ${LABELS}

			_EDIT_COORDS=($(box_coords_get MSG_BOX))

			[[ ${_MSG_KEY} == 'q' ]] && exit_request
			if [[ ${_MSG_KEY} == 'e' ]];then
				NDX=0
				for F in ${FIELDS};do
					((NDX++))
					LBL=$(cut -d'|' -f${NDX} <<<${LABELS} | cut -d':' -f1 | sed 's/<D> //') # Derive label from MSG
					VAL=$(cut -d':' -f2 <<<${F})
					_EDIT_COORDS=($(box_coords_get MSG_BOX))
					X=$(( _EDIT_COORDS[X] + NDX )) && Y=$(( _EDIT_COORDS[Y] + 3 ))
					NEW_VAL=$(edit_field "${LBL:gs/#/ /}" "${VAL} " ${X} ${Y}) # Quirky; adding a space after val seems to help coords stick
					FIELDS[${NDX}]="${LBL}:$(sed 's/ *$//' <<<${NEW_VAL})"
					msg_box -q -c -x${BOX_X} -y${BOX_Y} -w 60 $(msg_list_data ${FIELDS})
					sleep .1
				done
				tags_update ${FIELDS}
			fi
			msg_box_clear
			tcup ${BOX_X} ${BOX_Y};echo -n "Working..."
		fi
	done
}

tags_flac () {
	local FILE=${1}
	local ARTIST=''
	local TITLE=''
	local ALBUM=''
	local TEST=''

	TEST=$(metaflac --show-all-tags ${FILE})

	grep -q 'ARTIST\|TITLE\|ALBUM' <<<${TEST}
	if [[ ${?} -ne 0 ]];then
		FILE=${FILE}
		ARTIST=None
		TITLE=None
		ALBUM=None
	else
		if grep -q 'ARTIST' <<<${TEST};then
			ARTIST=$(metaflac --show-tag=ARTIST ${FILE} | cut -d'=' -f2-)
		fi
		if grep -q 'TITLE' <<<${TEST};then
			TITLE=$(metaflac --show-tag=TITLE ${FILE} | cut -d'=' -f2-)
		fi
		if grep -q 'ALBUM' <<<${TEST};then
			ALBUM=$(metaflac --show-tag=ALBUM ${FILE} | cut -d'=' -f2-)
		fi
	fi

	_EDITS[FILE]="${FILE}"
	_EDITS[ARTIST]="${ARTIST:=None}"
	_EDITS[TITLE]="${TITLE:=None}"
	_EDITS[ALBUM]="${ALBUM:=None}"
}

tags_mp3 () {
	local FILE=${1}
	local ARTIST=''
	local TITLE=''
	local ALBUM=''

	ARTIST=$(mp3info -p "%a" ${FILE})
	TITLE=$(mp3info -p "%t" ${FILE})
	ALBUM=$(mp3info -p "%l" ${FILE})

	_EDITS[FILE]="${FILE}"
	_EDITS[ARTIST]="${ARTIST:=None}"
	_EDITS[TITLE]="${TITLE:=None}"
	_EDITS[ALBUM]="${ALBUM:=None}"
}

tags_update () {
	local -a FIELDS=(${@})
	local FN
	local LBL
	local VAL
	local IN_FLAC=false
	local IN_MP3=false
	local IN_WAV=false
	local -a MSGS

	clear
	for F in ${FIELDS};do
		MSGS+=${F}
	done
	msg_box -p -H1 -P"Update (y/n)?" "<w>Update Tag<N>|$(msg_list_data ${MSGS})"

	IN_FLAC=false
	IN_MP3=false
	IN_WAV=false
	for F in ${FIELDS};do
		LBL=$(cut -d':' -f1 <<<${F:gs/#//})
		VAL=$(cut -d':' -f2 <<<${F})
		if [[ ${LBL} == 'FILE' ]];then
			case ${VAL:e} in
				flac) IN_FLAC=true; FN=${VAL};;
				mp3) IN_MP3=true; FN=${VAL};;
				wav) IN_WAV=true; FN=${VAL};;
			esac
		fi

		if [[ ${IN_FLAC} == 'true' ]];then
			metaflac --remove-tag ${LBL} ${FN}
			metaflac --set-tag ${LBL}=${VAL} ${FN}
		elif [[ ${IN_MP3} == 'true' || ${IN_WAV} == 'true' ]];then
			case ${LBL} in
				ALBUM) id3v2 -A ${VAL} ${FN};;
				ARTIST) id3v2 -a ${VAL} ${FN};;
				TITLE) id3v2 -t ${VAL} ${FN};;
			esac
		fi
	done
}

set_tags () {
	local TAG=${1}
	local TEXT=${2}
	local EXT=''
	local F

	for F in ${_FILE_LIST};do
		EXT=${F:e}
		case ${EXT} in
			mp3) case ${TAG} in
						a) mp3info -a ${TEXT} ${F};; # Set artist
						l) mp3info -l ${TEXT} ${F};; # Set album
					esac
					;;
			flac) case ${TAG} in
						a) metaflac --remove-tag "ARTIST" ${F};
						   metaflac --set-tag "ARTIST=${TEXT}" ${F}
							;;
						l) metaflac --remove-tag "ALBUM" ${F};
						   metaflac --set-tag "ALBUM=${TEXT}" ${F}
							;;
					esac
					;;
			wav) case ${TAG} in
						a) id3v2 -a ${TEXT} ${F};;
						l) id3v2 -A ${TEXT} ${F};;
					esac
					;;
			*) exit_leave "${EXT} not supported";;
		esac
	done
}

#--Begin GetOpts--
local -a OPTIONS
local OPTION
local DESC

read -r -d '' DESC<<EOF
Display and edit tags for audio files. Single files can be
\taccessed with a <FILE> argument. Otherwise, all files in
\tthe current directory will be processed.  Optionally,
\tset the Artist and/or Album tag.
EOF

parse_opts () {
	local OPTS=${@}
	local -a OPTSTR
	local LETTER_OPT
	local O

	for O in {1..${#OPTS}};do
		[[ ${OPTS[${O}]} =~ '[A-Za-z]' ]] && LETTER_OPT=${OPTS[${O}]}
		[[ ${O} -eq 1 && ${OPTS[${O}]} == ":" ]] && continue
		[[ ${O} -gt 1 && ${OPTS[${O}]} == ":" ]] && OPTSTR+=$(opt_type ${LETTER_OPT}) && continue
		OPTSTR+="-${OPTS[${O}]}"
	done
	echo ${OPTSTR}
}

opt_exists () {
	local OPT=${1}
	[[ $(( $OPTIONS[(Ie)${OPT}] )) -ne 0 ]] && return 0 || return 1
}

opt_type () {
	local OPT=${1}
	case ${OPT} in
		d) echo "<DIR>";;
		f) echo "<FILE>";;
		a) echo "<TEXT>";;
		l) echo "<TEXT>";;
	esac
}

usage () {
	local -a PENDING_MSGS=(${@})
	local OPTS=$(parse_opts ${OPTSTR})

	[[ ${_SMCUP} == 'true' ]] && do_rmcup

	tabs 5
	(
	echo -e "\n${WHITE_FG}\nUsage${RESET}: ${_SCRIPT} ${WHITE_FG}[${RESET}${OPTS}${WHITE_FG}]${RESET} [$(opt_type f)]\n"
	echo -e "${WHITE_FG}-H${RESET} help"
	echo -e "${WHITE_FG}-D${RESET} debug (repeats increase level)"
	echo -e "${WHITE_FG}-a${RESET} $(opt_type a) set artist tag (mp3 only)"
	echo -e "${WHITE_FG}-l${RESET} $(opt_type l) set album tag (mp3 only)"
	echo -e "\n${WHITE_FG}DESC${RESET}:${DESC}"
	) >&2

	exit_leave ${PENDING_MSGS}
}

OPTSTR=":HDa:l:"
OPTIND=0

ARTIST=''
ALBUM=''

while getopts ${OPTSTR} OPTION;do
	case $OPTION in
     H) usage;;
     D) dbg_set_level;;
     a) ARTIST=${OPTARG};;
     l) ALBUM=${OPTARG};;
     :) print -u2 "${RED_FG}${_SCRIPT}${RESET}: option: -${OPTARG} requires an argument"; usage;;
    \?) print -u2 "${RED_FG}${_SCRIPT}${RESET}: unknown option -${OPTARG}"; usage;;
	esac
	[[ ${OPTION} != 'D' ]] && OPTIONS+=${OPTION}
done
shift $(( OPTIND - 1 ))
#--End GetOpts--
 
# Execution
[[ ${_DEBUG} -eq 0 ]] && do_smcup

DEP=$(dep_exists mp3info id3v2 metaflac)
[[ ${?} -ne 0 ]] && exit_leave 1 $(err_msg_exit E "Missing dependency:${DEP}")

[[ ${#} -ne 0 ]] && FN=${1} || FN=''

if [[ -z ${FN} ]];then
	# Find audio files
	_FILE_LIST=("${(f)$(find . -maxdepth 1 -name '*flac' -type f)}")
	_FILE_LIST+=("${(f)$(find . -maxdepth 1 -name '*mp3' -type f)}")
	_FILE_LIST+=("${(f)$(find . -maxdepth 1 -name '*wav' -type f)}")
else
	[[ ! -e ${FN} ]] && exit_leave "${_SCRIPT_TAG} ${BOLD}${RED_FG}File not found${RESET}:${FN}"
	_FILE_LIST=(${FN})
fi

if ! arr_is_populated "${_FILE_LIST}";then
	exit_leave "${_SCRIPT_TAG} ${BOLD}${RED_FG}No audio files found${RESET}"
fi

[[ -n ${ARTIST} ]] && set_tags a ${ARTIST}
[[ -n ${ALBUM} ]] && set_tags l ${ALBUM}

tags_display
[[ ${_GOT_DATA} == 'false' ]] && exit_leave 1 $(err_msg_exit W "${#_FILE_LIST} audio files found but contain no tag data")

exit_leave
