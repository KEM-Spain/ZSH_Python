#!/usr/bin/zsh
# Inline ansi
BOLD="\033[1m"
ITALIC="\033[3m"
RESET="\033[m"
REVERSE="\033[7m"
STRIKE="\033[9m"
UNDER="\033[4m"

BLACK_BG="\033[40m"

BLUE_FG="\033[34m"
CYAN_FG="\033[36m"
GREEN_FG="\033[32m"
MAGENTA_FG="\033[35m"
RED_FG="\033[31m"
WHITE_FG="\033[37m"
YELLOW_FG="\033[33m"

# Declarations
typeset -A _UTILS
typeset -A _UTILS_DESC

# Constants
_UTILS=(
info 1
length 1
merge 1
rescale 1
dts2ac3 1
volume 1
)

_UTILS_DESC=(
info "INFO:Display video information"
length "LENGTH:Display video length"
merge "MERGE:Merge videos"
rescale "SCALE:Rescale videos"
dts2ac3 "AUDIO:Convert DTS audio to AC3"
volume "AUDIO:Increase volume by 3db"
)

_WRAPPER=${0:t}

# Imports
_LIB_DIR=${ZSH_LIB_DIR:=/usr/local/lib}
source ${_LIB_DIR}/LIB_INIT.zsh
source ${_LIB_DIR}/LIST.zsh
source ${_LIB_DIR}/SELECT.zsh
source ${_LIB_DIR}/LIB_DEPS.zsh

# Functions
info () {
	# Declarations
	local -A FLIST=()
	local -a LIST
	local BAR=''
	local BARLINE=0
	local HIT
	local TIT=''
	local DUR=''
	local VID=''
	local RES=''
	local AUD=''
	local SUB=''
	local LONGEST=0
	local NDX=0
	local K L

	# Constants
	local _VID_REGEX_Z='.*\.(mp4|avi|mkv)$'
	local _VID_REGEX_F='.*\.\(mp4\|avi\|mkv\)$'

	# Functions
	trim () {
		local LINE

		while read LINE;do
			echo ${LINE} | sed -e 's/^ *//' -e 's/ *$//'
		done
	}

	parse_info () {
		local LIST=("${(f)$(ffprobe ${1}  2>&1 | grep -i 'duration\|stream')}")
		local -a INFO
		local AUD=Audio:None
		local DESC
		local SDUR=''
		local DUR=Duration:None
		local HDR
		local RES=None
		local SUB=false
		local VAL
		local VID=Video:None
		local L

		for L in ${LIST};do
			[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} L: ${L}"
			[[ ${L} =~ 'attached pic' ]] && continue
			[[ ${L:l} =~ 'subtitle:' ]] && SUB=true
			[[ ${L:l} =~ 'duration:' ]] && DUR=$(perl -p -e 's/(\s+)([^,]+)(.*)/$2/i' <<<${L})
			[[ ${L:l} =~ "video:" ]] && RES=$(perl -p -e 's/^(.*)(?<=[ ,])(\d+x\d+)(?=[ ,])(.*)$/$2/g' <<<${L})
			[[ ${L:l} =~ "video:" ]] && VID=$(perl -pe 's/^(.*)(vid.*?)(?=[,(])(.*)/$2/i' <<<${L})
			[[ ${L:l} =~ "audio:" ]] && AUD=$(perl -pe 's/^(.*)(aud.*?)(?=[,(])(.*)/$2/i' <<<${L})
		done

		[[ ${SUB} == 'true' ]] && SUB="yes" || SUB="no"
		[[ ${AUD:l} != 'audio:none' ]] && AUD=${AUD:u}

		SDUR=$(cut -d: -f2- <<<${DUR} | trim | tr -d '[:.]')
		DUR=$(cut -d: -f2- <<<${DUR} | trim)
		RES=$(cut -d: -f2- <<<${RES} | trim)
		VID=$(cut -d: -f2- <<<${VID} | trim)
		AUD=$(cut -d: -f2- <<<${AUD} | trim)

		FLIST[${SDUR}:${@:t}]="${DUR}|${(U)VID}|${RES}|${(U)AUD}|${(C)SUB}"
	}

	#--Begin GetOpts--
	local DESC
	local -a OPTIONS

	read -r -d '' DESC<<___EOF
Provide essential info about video files
___EOF

	parse_opts () {
		local OPTS=${@}
		local -a OPTSTRING
		local LETTER_OPT
		local O

		for O in {1..${#OPTS}};do
			[[ ${OPTS[${O}]} =~ '[a-z]' ]] && LETTER_OPT=${OPTS[${O}]}
			[[ ${O} -eq 1 && ${OPTS[${O}]} == ":" ]] && continue
			[[ ${O} -gt 1 && ${OPTS[${O}]} == ":" ]] && OPTSTRING+=$(opt_type ${LETTER_OPT}) && continue
			OPTSTRING+="-${OPTS[${O}]}"
		done
		echo ${OPTSTRING}
	}

	opt_exists () {
		local OPT=${1}
		[[ $(( $OPTIONS[(Ie)${OPT}] )) -ne 0 ]] && return 0 || return 1
	}

	opt_type () {
		local OPT=${1}
		case ${OPT} in
			d) echo "<DIR>";;
			f) echo "<FILE>";;
			t) echo "<FILE_TYPE>";;
		esac
	}

	usage () {
		local -a PENDING_MSGS=(${@})

		tabs 5
		[[ ${_SMCUP} == 'true' ]] && do_rmcup

		echo "${WHITE_FG}\nUsage${RESET}:${_WRAPPER} ${_SCRIPT} [-H]\n"
		echo "${WHITE_FG}Desc${RESET}:${DESC}"

		exit_leave ${PENDING_MSGS}
	}

	OPTSTR=":HD"
	OPTIND=0

	while getopts ${OPTSTR} OPTION;do
		case $OPTION in
		  H) usage;;
		  D) dbg_set_level;;
		  :) print -u2 "${RED_FG}${_SCRIPT}${RESET}: option: -${OPTARG} requires an argument"; usage;;
		 \?) print -u2 "${RED_FG}${_SCRIPT}${RESET}: ${BOLD}${RED_FG}Unknown option${RESET} -${OPTARG}"; usage;;
		esac
		[[ ${OPTION} != 'D' ]] && OPTIONS+=${OPTION}
	done
	shift $(( OPTIND - 1 ))
	#--End GetOpts--

	# Execution
	[[ ${_DEBUG} -eq 0 ]] && do_smcup

	if [[ ${#} -eq 0 ]];then
		LIST=("${(f)$(find . -maxdepth 1 -type f -regex "${_VID_REGEX_F}")}")
	else
		LIST=(${@})
	fi

	HIT=false
	for L in ${LIST};do
		[[ ! ${L} =~ ${_VID_REGEX_Z} ]] && continue
		[[ -d ${L} ]] && continue
		HIT=true
		parse_info ${L}
		[[ ${#L} -gt ${LONGEST} ]] && LONGEST=${#L}
	done

	[[ ${HIT} == 'false' ]] && exit_leave "No video files found in ${PWD}/${@}"

	tcup 0 0
	printf "\n${WHITE_FG}%-*s %11s %5s %10s %5s %8s${RESET}\n" ${LONGEST} "Title" "Duration" "Video" "Resolution" "Audio" "Subtitle"
	printf "${MAGENTA_FG}%-*s %11s %5s %10s %5s %8s${RESET}\n" ${LONGEST} $(str_unicode_line ${LONGEST}) $(str_unicode_line 11) $(str_unicode_line 5) $(str_unicode_line 10) $(str_unicode_line 5) $(str_unicode_line 8)
	NDX=0
	for K in ${(Onk)FLIST};do
		((NDX++))
		TIT=$(cut -d':' -f2 <<<${K})
		DUR=$(cut -d'|' -f1 <<<${FLIST[${K}]})
		VID=$(cut -d'|' -f2 <<<${FLIST[${K}]})
		RES=$(cut -d'|' -f3 <<<${FLIST[${K}]})
		AUD=$(cut -d'|' -f4 <<<${FLIST[${K}]})
		SUB=$(cut -d'|' -f5 <<<${FLIST[${K}]})
		BARLINE=$(( NDX % 2 )) # Barlining 
		[[ ${BARLINE} -ne 0 ]] && BAR=${BLACK_BG} || BAR="" # Barlining
		printf "${BAR}${WHITE_FG}%*s ${CYAN_FG}${BOLD}${ITALIC}%11s${RESET}${BAR}${CYAN_FG} %5s %10s %5s %8s${RESET}\n" ${LONGEST} ${TIT} ${DUR} ${VID} ${RES} ${AUD} ${SUB}
	done | mypager wait

}

length () {
	local -a FILELIST=()
	local -a FN_ARG=()
	local -a LIST=()
	local -a PRELIST=()
	local DESC=''
	local DNUM=0
	local DUR=''
	local F=''
	local HDR=''
	local HIT=false
	local LONGEST=0
	local NDX=0
	local TITLE=''
	local NO_HDR=false

	read -r -d '' DESC<<___EOF
Display video duration
\tOptions: -h No header
___EOF

	# Functions
	usage () {
		local -a PENDING_MSGS=(${@})

		tabs 5
		[[ ${_SMCUP} == 'true' ]] && do_rmcup

		echo "${WHITE_FG}\nUsage${RESET}:${_WRAPPER} ${_SCRIPT} [-H] <VIDEO_FILES>\n"
		echo "${WHITE_FG}Desc${RESET}:${DESC}"

		echo "\n$(msg_exit W ${PENDING_MSGS})"
		exit_leave
	}

	# Execution
	[[ ${1} == '-H' ]] && usage
	if [[ ${1} == '-h' ]];then
		NO_HDR=true
		shift
	else
		NO_HDR=false
	fi
	[[ ${#} -eq 0 ]] && FN_ARG=${PWD} || FN_ARG=(${@})
	
	if [[ ${FN_ARG} == ${PWD} ]];then
		FILELIST=("${(f)$(find ${PWD} -maxdepth 2 -type f ! -path ${PWD})}")
	else
		FILELIST=${FN_ARG}
	fi

	for F in ${FILELIST};do
		((NDX++))
		[[ ! -f ${F} ]] && continue
		file ${F} | grep -qi 'media\|matroska\|video'
		[[ ${?} -ne 0 ]] && echo -n "Searching ${NDX}" && continue
		[[ ${#F:t} -gt ${LONGEST} ]] && LONGEST=${#F:t}
		PRELIST+=${F}
		HIT=true
	done

	[[ ${HIT} == 'false' ]] && exit_leave "No video files found"

	[[ ${#LIST} -ne 0 ]] && echo "\n${CYAN_FG}Video duration info...${RESET}"

	for F in ${PRELIST};do
		DUR=$(ffmpeg -i "${F}"  2>&1 | grep Duration | cut -d':' -f2- | cut -d',' -f1)
		DNUM=$(sed 's/://g' <<<${DUR})
		LIST+="${DNUM}|${DUR}|${F:t}"
	done

	if [[ ${NO_HDR} == 'false' ]];then
		[[ ${#F} -gt 1 ]] && HDR="Titles in Descending Length"
		[[ -n ${HDR} ]] && echo "\n${MAGENTA_FG}${HDR}${CYAN_FG}" && echo $(str_unicode_line ${#HDR}) && echo -n "${RESET}"
	fi

	for F in ${(On)LIST};do
		TITLE=$(cut -d'|' -f3 <<<${F})
		DUR=$(cut -d'|' -f2 <<<${F})
		printf "${WHITE_FG}%*s${RESET}:%.9s\n" ${LONGEST} ${TITLE} ${DUR}
	done
}

volume () {
	local DESC=''
	local -a FN_ARG=()
	local DB=''
	local LONGEST=0
	local LIST=()
	local F=''

	read -r -d '' DESC<<___EOF
Increase video volume by 3db
___EOF

	# Functions
	usage () {
		local -a PENDING_MSGS=(${@})

		tabs 5
		[[ ${_SMCUP} == 'true' ]] && do_rmcup

		echo "${WHITE_FG}\nUsage${RESET}:${_WRAPPER} ${_SCRIPT} [-H] <VIDEO_FILES>\n"
		echo "${WHITE_FG}Desc${RESET}:${DESC}"

		echo "\n$(msg_exit W ${PENDING_MSGS})"
		exit_leave
	}

	# Execution
	[[ ${1} == '-H' ]] && usage
	[[ ${#} -eq 0 ]] && usage "${_SCRIPT_TAG} ${RED_FG}Missing required argument${RESET}:<VIDEO FILE>" || FN_ARG=(${@})
	[[ ! -f ${FN_ARG} ]] && exit_leave $(msg_exit E "File not found:${FN_ARG}")
	
	file ${FN_ARG} | grep -qi 'media\|matroska\|video'
	[[ ${?} -ne 0 ]] && exit_leave $(msg_exit E "File not a video:${FN_ARG}")

	echo "\n${CYAN_FG}Increasing volume by 3dB...${RESET}"
	ffmpeg -i "${FN_ARG}" -af "volume=3dB" ${FN_ARG:r}_v+3d.${FN_ARG:e}
}

merge () {
	# Declarations
	local -a FILES_TO_MERGE=()
	local -a FILELIST=()
	local -A ORIGINALS=()
	local APP=ffmpeg
	local DESTINATION_FILE
	local F FN
	local RESPONSE

	# Constants
	local _OUT_FILE=vid_merge
	local _VID_REGEX_F='.*\.\(mp4\|avi\|mkv\)$'

	#--Begin GetOpts--
	local DESC
	local -a OPTIONS
	local OPTSTR

	read -r -d '' DESC<<___EOF
Merge video files
\tDefault merge tool is ffmpeg
\tOptionally, choose other tools: melt or mkvmerge
\tIf no destination file is given, the default "vid_merge" will be assigned
\tA single file <GLOB> is accepted to limit the candidates.  If no argument is
\tprovided, all video files in PWD are candidates.
___EOF

	parse_opts () {
		local OPTS=${@}
		local -a OPTSTRING
		local LETTER_OPT
		local O

		for O in {1..${#OPTS}};do
			[[ ${OPTS[${O}]} =~ '[a-z]' ]] && LETTER_OPT=${OPTS[${O}]}
			[[ ${O} -eq 1 && ${OPTS[${O}]} == ":" ]] && continue
			[[ ${O} -gt 1 && ${OPTS[${O}]} == ":" ]] && OPTSTRING+=$(opt_type ${LETTER_OPT}) && continue
			OPTSTRING+="-${OPTS[${O}]}"
		done
		echo ${OPTSTRING}
	}

	opt_exists () {
		local OPT=${1}
		[[ $(( $OPTIONS[(Ie)${OPT}] )) -ne 0 ]] && return 0 || return 1
	}

	opt_type () {
		local OPT=${1}
		case ${OPT} in
			d) echo "<DIR>";;
			f) echo "<FILE>";;
			l) echo "<GLOB>";;
			t) echo "<FILE_TYPE>";;
		esac
	}

	usage () {
		local -a PENDING_MSGS=(${@})

		tabs 5
		[[ ${_SMCUP} == 'true' ]] && do_rmcup

		echo "${WHITE_FG}\nUsage${RESET}:${_WRAPPER} ${_SCRIPT} [$(opt_type l)]\n"
		echo "${WHITE_FG}-H${RESET} help"
		echo "${WHITE_FG}-m${RESET} use melt"
		echo "${WHITE_FG}-k${RESET} use mkvmerge"
		echo "${WHITE_FG}-o $(opt_type f)${RESET} output file name"
		echo "${WHITE_FG}\nDesc${RESET}:${DESC}"

		echo "\n$(msg_exit W ${PENDING_MSGS})"
		exit_leave
	}

	OPTSTR=":HDkmo:"

	OPTIND=0
	while getopts ${OPTSTR} OPTION;do
		case $OPTION in
		  H) usage;;
		  D) DEBUG=true;;
		  m) APP=melt;;
		  k) APP=mkvmerge;;
		  o) _OUT_FILE=${OPTARG};;
		  :) print -u2 "${SCRIPT}: option: -${OPTARG} requires an argument"; usage;;
		 \?) print -u2 "${SCRIPT}: ${BOLD}${RED_FG}Unknown option${RESET} -${OPTARG}"; usage;;
		esac
	done
	shift $(( OPTIND - 1 ))
	#--End GetOpts--

	#--Execution--
	if [[ ${#} -eq 0 ]];then
		FILELIST=(eval "${(f)$(find ${PWD} -maxdepth 1 -type f -regex "${_VID_REGEX_F}" ! -path ${PWD})}") # Pull list from current dir
	else
		local -a CMD=($(path_get_raw_cmdline))
		local FGLOB=${CMD[-1]}
		FILELIST=(eval "${(f)$(find ${PWD} -maxdepth 1 -type f -name ${FGLOB} ! -path ${PWD})}") # Pull list from current dir
	fi

	[[ -z ${FILELIST} ]] && exit_leave "${_SCRIPT_TAG} ${RED_FG}Missing required arguments${RESET}:<FILES_TO_MERGE>"
	
	for F in ${FILELIST};do
		[[ ! ${$(file ${F}):l} =~ 'media' ]] && continue
		grep -q "${_OUT_FILE}\|vid_merge" <<<${F}
		[[ ${?} -eq 0 ]] && continue
		# Merging has problems with some file names - simplify
		FN=$(fsub -Cs <<<${F})
		mv -f ${F} ${FN} >/dev/null 2>&1
		FILES_TO_MERGE+=${FN:t}
		ORIGINALS[${FN}]=${F}
	done

	for F in ${FILES_TO_MERGE};do 
		[[ ! -e ${F} ]] && exit_leave $(msg_exit E "file:${F} not found")
		file ${F} | grep -qi 'media\|matroska\|video'
		[[ ${?} -ne 0 ]] && exit_leave $(msg_exit E "file:${F} is not of type video")
	done

	[[ -z ${DESTINATION_FILE} ]] && DESTINATION_FILE=${_OUT_FILE}.${FILES_TO_MERGE[1]:e} || DESTINATION_FILE=${_OUT_FILE}

	echo "Application for merge:${BOLD}${ITALIC}${APP}${RESET}"
	echo "Files to merge:\n${BOLD}${ITALIC}$(for F in ${FILES_TO_MERGE};do echo ${F};done)${RESET}"
	echo "Output file:${BOLD}${ITALIC}${DESTINATION_FILE}${RESET}"
	respond "${CYAN_FG}Continue${RESET}"
	if [[ ${?} -ne 0 ]];then
		local K
		for K in ${(k)ORIGINALS};do
			mv -f "${K}" "${ORIGINALS[${K}]}"
		done
		exit_leave "[${WHITE_FG}${0}${RESET}] ${RED_FG}Operation Canceled${RESET}"
	fi

	if overwrite_file ${DESTINATION_FILE};then
		/bin/rm -f ${DESTINATION_FILE} >/dev/null 2>&1
	else
		exit_leave "[${WHITE_FG}${0}${RESET}] ${RED_FG}Operation Canceled${RESET}" || echo
	fi

	echo "\nMerging:${WHITE_FG}${#FILES_TO_MERGE} files ${RESET} to ${WHITE_FG}${DESTINATION_FILE}${RESET} using APP:${WHITE_FG}${APP}${RESET}"
	case ${APP} in
		ffmpeg)		[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} ffmpeg -loglevel panic -f concat -safe 0 -i <(for F in ${FILES_TO_MERGE}; do echo "file '${PWD}/${F}'"; done) -c copy ${DESTINATION_FILE}"
						ffmpeg -loglevel error -f concat -safe 0 -i <(for F in ${FILES_TO_MERGE}; do echo "file '${PWD}/${F}'"; done) -c copy ${DESTINATION_FILE}
						;;
		melt)			[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} melt ${FILES_TO_MERGE} -consumer avformat:${DESTINATION_FILE} acodec=libmp3lame vcodec=libx264 -progress"
						melt ${FILES_TO_MERGE} -consumer avformat:${DESTINATION_FILE} acodec=libmp3lame vcodec=libx264 -progress
						;;
		mkvmerge)	[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} mkvmerge -o ${DESTINATION_FILE} ${FILES_TO_MERGE}"
						eval "mkvmerge -o ${DESTINATION_FILE} ${FILES_TO_MERGE}"
						;;
	esac
}

rescale () {
	# Declarations
	local -a RESOLUTIONS
	local SCALE
	local FN_OUT
	local FN

	# Constants
	local RESOLUTIONS=(1920:1080 1280:720)

	#--Begin GetOpts--
	local -a OPTIONS
	local OPTSTR
	local DESC

	read -r -d '' DESC<<___EOF
Rescale video files
___EOF

	parse_opts () {
		local OPTS=${@}
		local -a OPTSTRING
		local LETTER_OPT
		local O

		for O in {1..${#OPTS}};do
			[[ ${OPTS[${O}]} =~ '[a-z]' ]] && LETTER_OPT=${OPTS[${O}]}
			[[ ${O} -eq 1 && ${OPTS[${O}]} == ":" ]] && continue
			[[ ${O} -gt 1 && ${OPTS[${O}]} == ":" ]] && OPTSTRING+=$(opt_type ${LETTER_OPT}) && continue
			OPTSTRING+="-${OPTS[${O}]}"
		done
		echo ${OPTSTRING}
	}

	opt_exists () {
		local OPT=${1}
		[[ $(( $OPTIONS[(Ie)${OPT}] )) -ne 0 ]] && return 0 || return 1
	}

	opt_type () {
		local OPT=${1}
		case ${OPT} in
			d) echo "<DIR>";;
			f) echo "<FILE>";;
			t) echo "<FILE_TYPE>";;
			v) echo "<VIDEO_FILE>";;
		esac
	}

	usage () {
		local OPTS=$(parse_opts ${OPTSTR})
		local -a PENDING_MSGS=(${@})

		tabs 5
		[[ ${_SMCUP} == 'true' ]] && do_rmcup

		echo -e "\n${WHITE_FG}Usage${RESET}:${_WRAPPER} ${_SCRIPT} ${WHITE_FG}[${RESET}${OPTS}${WHITE_FG}]${RESET} $(opt_type v)\n"
		echo -e "${WHITE_FG}-H${RESET} help"
		echo -e "${WHITE_FG}-D${RESET} debug"
		echo -e "${WHITE_FG}\nDesc${RESET}:${DESC}"

		echo "\n$(msg_exit W ${PENDING_MSGS})"
		exit_leave
	}

	OPTSTR=":HD"
	OPTIND=0

	while getopts ${OPTSTR} OPTION;do
		case $OPTION in
		  H) usage;;
		  D) dbg_set_level;;
		  :) print -u2 "${RED_FG}${_SCRIPT}${RESET}: option: -${OPTARG} requires an argument"; usage;;
		 \?) print -u2 "${RED_FG}${_SCRIPT}${RESET}: ${BOLD}${RED_FG}Unknown option${RESET} -${OPTARG}"; usage;;
		esac
		[[ ${OPTION} != 'D' ]] && OPTIONS+=${OPTION}
	done
	shift $(( OPTIND - 1 ))
	#--End GetOpts--

	# Execution
	[[ ${_DEBUG} -eq 0 ]] && do_smcup
	[[ ${#} -eq 0 ]] && usage "${_SCRIPT_TAG} ${RED_FG}Missing argument${RESET}:$(opt_type v)" || FN=${1}

	[[ ! -e ${FN} ]] && usage "File not found:${FN}"

	sel_set_list ${RESOLUTIONS}
	sel_list -sa -O${CYAN_FG} -x10 -y30 -F"<w>Select Resolution<N>" -H"Video Utilities"
	[[ ${?} -ne 0 ]] && exit_leave "[${WHITE_FG}${0}${RESET}] ${RED_FG}Operation Canceled${RESET}"
	SCALE=${_SEL_VAL}

	FN_OUT="${FN:r} ${SCALE}.mkv"
	[[ -e ${FN_OUT} ]] && exit_leave $(msg_exit W "File naming confict: ${FN_OUT}")

	ffmpeg -i ${FN} -vf scale=${SCALE} ${FN_OUT}
}


dts2ac3 () {
	local DESC
	local FN
	
	read -r -d '' DESC<<___EOF
Convert DTS audio to AC3
___EOF

	# Functions
	usage () {
		local -a PENDING_MSGS=(${@})

		tabs 5
		[[ ${_SMCUP} == 'true' ]] && do_rmcup

		echo "${WHITE_FG}\nUsage${RESET}:${_WRAPPER} ${_SCRIPT} [-H] <VIDEO_FILE>\n"
		echo "${WHITE_FG}Desc${RESET}:${DESC}"

		echo "\n$(msg_exit W ${PENDING_MSGS})"
		exit_leave
	}

	# Execution
	[[ ${1} == '-H' ]] && usage

	[[ ${#} -eq 0 ]] && usage "${_SCRIPT_TAG} ${RED_FG}Missing required argument${RESET}:<VIDEO_FILE>" || FN=${1}
	[[ ! -e ${FN} ]] && exit_leave $(msg_exit E "file not found:${FN}")

	ffmpeg -i ${FN} -map 0 -map 0:a -c copy -c:a:1 ac3 -b:a:1 640k ${FN:r}_ac3.mkv
}

#--Begin GetOpts--
local -a OPTIONS
local OPTION

read -r -d '' DESC<<EOF
Execute system utilities
\t------------------------
$(for K in ${(ok)_UTILS_DESC};do
printf "${WHITE_FG}%-14s${RESET} %s\n" ${K} ${_UTILS_DESC[${K}]}
done)
EOF

parse_opts () {
	local OPTS=${@}
	local -a OPTSTRING
	local LETTER_OPT
	local O

	for O in {1..${#OPTS}};do
		[[ ${OPTS[${O}]} =~ '[a-z]' ]] && LETTER_OPT=${OPTS[${O}]}
		[[ ${O} -eq 1 && ${OPTS[${O}]} == ":" ]] && continue
		[[ ${O} -gt 1 && ${OPTS[${O}]} == ":" ]] && OPTSTRING+=$(opt_type ${LETTER_OPT}) && continue
		OPTSTRING+="-${OPTS[${O}]}"
	done
	echo ${OPTSTRING}
}

opt_exists () {
	local OPT=${1}
	[[ $(( $OPTIONS[(Ie)${OPT}] )) -ne 0 ]] && return 0 || return 1
}

opt_type () {
	local OPT=${1}
	case ${OPT} in
		u) echo "<UTIL>";;
	esac
}

usage () {
	local -a PENDING_MSGS=(${@})
	local OPTS=$(parse_opts ${OPTSTR})

	[[ ${_SMCUP} == 'true' ]] && do_rmcup

	tabs 5
	echo -e "\n${WHITE_FG}\nUsage${RESET}:${_SCRIPT} ${WHITE_FG}[${RESET}${OPTS}${WHITE_FG}]${RESET} $(opt_type u)\n"
	echo -e "${WHITE_FG}-H${RESET} help"
	echo -e "${WHITE_FG}-D${RESET} debug (repeats increase level)"
	echo -e "\n${WHITE_FG}\nDesc${RESET}:${DESC}"

	echo "\n$(msg_exit W ${PENDING_MSGS})"
	exit_leave
}

OPTSTR=":HD"
OPTIND=0

UTIL=none

while getopts ${OPTSTR} OPTION;do
	case $OPTION in
		H) usage;;
		D) dbg_set_level;;
		:) print -u2 "${RED_FG}${_SCRIPT}${RESET}: option: -${OPTARG} requires an argument"; usage;;
		\?) print -u2 "${RED_FG}${_SCRIPT}${RESET}: ${BOLD}${RED_FG}Unknown option${RESET} -${OPTARG}"; usage;;
	esac
	[[ ${OPTION} != 'D' ]] && OPTIONS+=${OPTION}
done
shift $(( OPTIND - 1 ))
#--End GetOpts--

# Execution
[[ ${#} -ne 0 ]] && UTIL=${1:=none}

[[ ${UTIL} != 'none' && ${_UTILS[${UTIL}]} -ne 1 ]] && usage "UTIL:${UTIL} not found"

if [[ ${UTIL} != 'none' ]];then
	UTIL=${1}
	_SCRIPT=${UTIL}
	[[ ${_UTILS[${UTIL}]} -eq 1 ]] && shift && ${UTIL} ${@}
else
	[[ ${_DEBUG} -eq 0 ]] && do_smcup

	sel_set_list ${(v)_UTILS_DESC}
	sel_list -sa -O ${GREEN_FG} -C -F"<w>Show Video Utilities usage or <h>(q)uit<N>" -H"Video Utilities"

	_SCRIPT=${(k)_UTILS_DESC[(r)${_SEL_VAL}]}
	echo # Clear read
	${_SCRIPT} -H # Call function help
fi

exit_leave

