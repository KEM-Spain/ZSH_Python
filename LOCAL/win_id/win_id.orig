#!/usr/bin/zsh
# Inline ansi
BOLD="\033[1m"
ITALIC="\033[3m"
RESET="\033[m"
REVERSE="\033[7m"
STRIKE="\033[9m"
UNDER="\033[4m"

BLACK_BG="\033[40m"

BLUE_FG="\033[34m"
CYAN_FG="\033[36m"
GREEN_FG="\033[32m"
MAGENTA_FG="\033[35m"
RED_FG="\033[31m"
WHITE_FG="\033[37m"
YELLOW_FG="\033[33m"

# Imports
_LIB_DIR=${ZSH_LIB_DIR:=/usr/local/lib}
source ${_LIB_DIR}/LIB_INIT.zsh
source ${_LIB_DIR}/UTILS.zsh
source ${_LIB_DIR}/LIB_DEPS.zsh

# Declarations
# TODO: May be possible to avoid PID searches and rely solely on wmctrl content
typeset -A ALT_PID_TAGS=(audacity /bin/audacity brave-browser "brave --force" com.obsproject.studio obs firefox_firefox firefox haruna_haruna bin/haruna musescore mscore4portable chrome store=basic enpass opt/enpass)
typeset -A SINGLE_WIN_APPS=(com.obsproject.studio 1 simplescreenrecorder 1 deluge 1 vlc 1 chrome 1 haruna 1 viber 1 pycharm 1)
typeset -a WIN_INFO

# Constants
_LOG=/tmp/${0:t}.log
MAX_CYCLES=25 # Max attempts to get an id
MAX_CHANGES=3 # Some apps open preliminary windows and id's change - 3 changes should be sufficient
STABLE_ID=5 # iterations without a change of id should indicate a stable id


#--Begin GetOpts--
local -a OPTIONS
local OPTION
local DESC

read -r -d '' DESC<<EOF
Request a window ID for given application or the current window
\tAn application is specified by a string fully or partially matching the window title
\tIf no string is passed, the application is assumed to be the terminal
\tNote: Use HEX_ID for wmctrl, DEC_ID for xdotool
\tReturn format:HEX ID|DEC ID|TITLE
EOF

parse_opts () {
	local OPTS=${@}
	local -a OPTSTR
	local LETTER_OPT
	local O

	for O in {1..${#OPTS}};do
		[[ ${OPTS[${O}]} =~ '[a-z]' ]] && LETTER_OPT=${OPTS[${O}]}
		[[ ${O} -eq 1 && ${OPTS[${O}]} == ":" ]] && continue
		[[ ${O} -gt 1 && ${OPTS[${O}]} == ":" ]] && OPTSTR+=$(opt_type ${LETTER_OPT}) && continue
		OPTSTR+="-${OPTS[${O}]}"
	done
	echo ${OPTSTR}
}

opt_exists () {
	local OPT=${1}
	[[ $(( $OPTIONS[(Ie)${OPT}] )) -ne 0 ]] && return 0 || return 1
}

opt_type () {
	local OPT=${1}
	case ${OPT} in
		a) echo "<APP_STRING>";;
		d) echo "<DIR>";;
		f) echo "<FILE>";;
	esac
}

usage () {
	local -a PENDING_MSGS=(${@})
	local OPTS=$(parse_opts ${OPTSTR})

	[[ ${_SMCUP} == 'true' ]] && do_rmcup

	tabs 5
	(
	echo -e "\n${WHITE_FG}\nUsage${RESET}: ${_SCRIPT} ${WHITE_FG}[${RESET}${OPTS}${WHITE_FG}]${RESET} [$(opt_type a)]\n"
	echo -e "${WHITE_FG}-H${RESET} help"
	echo -e "${WHITE_FG}-D${RESET} debug (repeats increase level)"
	echo -e "${WHITE_FG}-q${RESET} quiet mode"
	echo -e "\n${WHITE_FG}Desc${RESET}:${DESC}"
	) >&2

	exit_leave ${PENDING_MSGS}
}

OPTSTR=":HDq"
OPTIND=0

QUIET=false

while getopts ${OPTSTR} OPTION;do
	case $OPTION in
     H) usage;;
     D) dbg_set_level;;
     q) QUIET=true;;
     :) print -u2 "${RED_FG}${_SCRIPT}${RESET}: option: -${OPTARG} requires an argument"; usage;;
    \?) print -u2 "${RED_FG}${_SCRIPT}${RESET}: unknown option -${OPTARG}"; usage;;
	esac
	[[ ${OPTION} != 'D' ]] && OPTIONS+=${OPTION}
done
shift $(( OPTIND - 1 ))
#--End GetOpts--
 
# Execution
if [[ ${#} -eq 0 ]];then
	ID=$(xdotool getactivewindow)
	printf "0x%x|%d|Terminal" ${ID} ${ID} # Active window will be terminal is executed from command line
	return 0
else
	APP=${1}
	logit ${_LOG} "Got APP:${APP}"
fi

# Handle any alternate tags
if [[ -n ${ALT_PID_TAGS[${APP:l}]} ]];then # Lowercase
	PID_TAG=${ALT_PID_TAGS[${APP:l}]}
	logit ${_LOG} "Got alternate PID_TAG:${PID_TAG} for APP:${APP}"
else
	PID_TAG=${APP:l}
fi

# Find PID
logit ${_LOG} "Probing for PID_TAG:${PID_TAG}"
for X in {1..10};do
	PID=$(ps aux | grep  -i ${PID_TAG[1,15]} | head -1 | tr -s '[:space:]' | cut -d' ' -f2) # Limit string len to pgrep max allowed
	[[ -n ${PID} ]] && break
	sleep .2
done
logit ${_LOG} "Finished probing for PID..."

if [[ -z ${PID} ]];then
	logit ${_LOG} "PID for PID_TAG:${PID_TAG} could NOT be found"
	return 1
else
	logit ${_LOG} "Got PID:${PID} for PID_TAG:${PID_TAG}"
fi

# Find WID
CYCLE=0
DEC_ID=''
ID_CHANGE=0
ID_CNT=0
LAST_WID=?
TIMEOUT=false
TITLE=''
WIN_INFO=''

logit ${_LOG} "Probing for WID with PID:${PID}"
MSG_OUT=false
while true;do
	((CYCLE++))
	[[ ${CYCLE} -ge ${MAX_CYCLES} ]] && TIMEOUT=true && break 

	WIN_INFO=$(wmctrl -lp | grep -i ${PID} | sort -r | head -1 | tr -s '[:space:]') # sorts by hex win_id - most recent is top
	if [[ -n ${WIN_INFO} ]];then
		WIN_ID=$(cut -d' ' -f1 <<<${WIN_INFO})
		TITLE=$(cut -d' ' -f4- <<<${WIN_INFO})
		if [[ ${MSG_OUT} == 'false' ]];then
			logit ${_LOG} "Got WIN_INFO:${WIN_INFO} WIN_ID:${WIN_ID} TITLE:${TITLE} for APP:${APP}"
			logit ${_LOG} "Verifying stability..."
			MSG_OUT=true
		fi
		[[ ${LAST_WID} != ${WDW_ID} ]] && LAST_WID=${WDW_ID} && ((ID_CHANGE++)) && ID_CNT=0 # Different id
		[[ ${LAST_WID} == ${WDW_ID} ]] && ((ID_CNT++)) # Same id
		if [[ (${SINGLE_WIN_APPS[${APP:l}]} -eq 1) || (${ID_CHANGE} -ge ${MAX_CHANGES}) || (${ID_CNT} -ge ${STABLE_ID}) ]];then
			[[ ${MSG_OUT} == 'true' ]] && logit ${_LOG} "WDW_ID:${WIN_ID} is stable"
			DEC_ID=$(printf "%d" ${WIN_ID})
			logit ${_LOG} "Returning HEX_ID:${WIN_ID}, DEC_ID:${DEC_ID}, TITLE:${TITLE}"
			echo "${WIN_ID}|${DEC_ID}|${TITLE}"
			return 0
		fi
		[[ ${ID_CHANGE} -eq 0 ]] && TIMEOUT=true && break
	fi
	sleep .2
done 
logit ${_LOG} "Finished probing for WID..."

if [[ -z ${WIN_ID} ]];then
	echo "Unable to locate WIN_ID for ${APP}"
	logit ${_LOG} "Failed to locate WIN_ID for ${APP}"
	return 1
fi

if [[ ${TIMEOUT} == 'true' ]];then
	logit ${_LOG} "Probing for WID with PID:${PID} timed out"
	return 1
fi
