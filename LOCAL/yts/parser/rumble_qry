#!/usr/bin/zsh
 
# Inline ansi
BOLD="\033[1m"
ITALIC="\033[3m"
RESET="\033[m"
REVERSE="\033[7m"
STRIKE="\033[9m"
UNDER="\033[4m"
BLACK_BG="\033[40m"
BLUE_FG="\033[34m"
CYAN_FG="\033[36m"
GREEN_FG="\033[32m"
MAGENTA_FG="\033[35m"
RED_FG="\033[31m"
WHITE_FG="\033[37m"
YELLOW_FG="\033[33m"
CSR_OFF="\033[?25l"
CSR_ON="\033[?25h"

# Imports
[[ -z ${ZSH_LIB_DIR} ]] && _LIB_DIR=/usr/local/lib || _LIB_DIR=${ZSH_LIB_DIR}
source ${_LIB_DIR}/LIB_INIT.zsh # Must be first
source ${_LIB_DIR}/UTILS.zsh
source ${_LIB_DIR}/LIB_DEPS.zsh # Must be last

# Constants
_CFG_DIR=${PWD}

# Declarations
#typeset -a AGES=()
#typeset -a DURATIONS=()
#typeset -a LINKS=()
#typeset -a TITLES=()
typeset -A _QUERY_MAP=(l live g general u user c channel)
typeset -A _QUERY_OPTS=()
typeset -a _RUMBLE_PAGE=()
typeset -A _RUMBLE_ROWS=()
typeset -F _TAB_SZ_CHECK=0
typeset -a _LOCAL_LIST=()

g_QUERY_KEY=''
g_DISPLAY_KEY=''
g_SEARCH_TYPE=''
_VIEWER=true
_MAX_TITLE=74
SEARCHTERM='qry_viewer'

# Functions
source <(func_print yts clean_title) # Get clean_title func

strip_space () {
	local LINE

	while read LINE;do
		tr -d '\011' | tr -s '\012' | sed '/^$/d' <<< ${LINE}
	done
}

strip_unwanted () {
	local LINE
	while read -r LINE;do
		[[ -z ${LINE} ]] && continue
		[[ ${LINE} =~ '^[Vv]erified$' ]] && continue
		[[ ${LINE} =~ '^LIVE$' ]] && continue
		LINE=$(tr -d '\011' <<< ${LINE})
		LINE=$(tr -d '\012' <<< ${LINE})
		LINE=$(sed 's/|/#/g' <<< ${LINE})
		LINE=$(iconv -c -f UTF-8 -t ASCII <<<${LINE} 2>/dev/null) # Convert UTF-8
		LINE=$(html2text -width 500 -ascii <<<${LINE} 2>/dev/null) # Convert any HTML 
		str_trim <<< ${LINE}
	done
}

parse_rumble () {
	local PARSE_TYPE=${1}
	local -A GENERAL=()
	local -A NON_GENERAL=()
	local -a HTML=()
	local DATA=''
	local LAST_ROW=0
	local LINE=''
	local NDX=0
	local OMIT_NDX=0
	local ROW=0
	local TAB=''
	local TAG=''
	local TAGLINE=''
	local TAG_ARRAY=''
	local AGE AUTHOR TITLE URL TIME AGETIME
	local K

	[[ -z ${_RUMBLE_PAGE} ]] && return 1 # No data returned from query

	NON_GENERAL[TITLES]='thumbnail__title attr{title}'
	NON_GENERAL[LINKS]='title__link attr{href}'
	NON_GENERAL[AGES]='videostream__time text{}'
	NON_GENERAL[DURATIONS]='videostream__badge text{}'

	GENERAL[AUTHORS]='ellipsis-1 text{}'
	GENERAL[TITLES]='video-item--title text{}'
	GENERAL[LINKS]='video-item--a attr{href}'
	GENERAL[AGES]='video-item--time text{}'
	GENERAL[DURATIONS]='video-item--duration attr{data-value}'

	if [[ ${PARSE_TYPE} == 'g' ]];then
		TAG_ARRAY="GENERAL"
		TAGLINE="\"video-item--img\""
	else
		TAG_ARRAY="NON_GENERAL"
		TAGLINE="thumbnail__grid--item"
	fi

	ROW=-1
	while read LINE;do
		HTML+=${LINE}
		if [[ ${LINE} =~ ${TAGLINE} ]];then
			((ROW++))
			[[ ${ROW} -eq 0 ]] && continue
			for K in ${(k)${(P)TAG_ARRAY}};do
				TAG=${${(P)TAG_ARRAY}[${K}]}
				DATA=$(pup --charset utf8 ".${TAG}" <<<${HTML} | strip_unwanted)
				_RUMBLE_ROWS[${ROW}_${K}]=${DATA}
			done
			HTML=()
		fi
	done <<<${_RUMBLE_PAGE}

	[[ -e ./row_data ]] && /bin/rm -f ./row_data

	LAST_ROW=0
	NDX=0
	_LOCAL_LIST=()
	for K in ${(onk)_RUMBLE_ROWS};do
		ROW=$(cut -d'_' -f1 <<<${K})
		TAB=$(cut -d'_' -f2 <<<${K})
		case ${TAB} in
			AGES) AGE=${_RUMBLE_ROWS[${K}]};;
			AUTHORS) AUTHOR=${_RUMBLE_ROWS[${K}]};;
			DURATIONS) TIME=${_RUMBLE_ROWS[${K}]};;
			LINKS) URL="https://rumble.com${_RUMBLE_ROWS[${K}]}";;
			TITLES) TITLE=${_RUMBLE_ROWS[${K}]};;
		esac
		if [[ -n ${AGE} && -n ${TITLE} && -n ${URL} && ${TIME} ]];then # All fields populated
			if [[ ${ROW} != ${LAST_ROW} ]];then
				# TODO: src_v1_mfp may need omission - investigate
				[[ ${URL} =~ 'ucp_f' ]] && OMIT_NDX=${ROW} && echo "\n${RED_FG}Omitting NDX:${ROW}${RESET} - ucp_f" >&2
				[[ ! ${TIME} =~ ':' ]] && OMIT_NDX=${ROW} && echo "\n${RED_FG}Omitting NDX:${ROW}${RESET} - TIME no colon" >&2
				AGETIME="(${AGE} ${TIME})"
				[[ -z ${AUTHOR} ]] && AUTHOR=${_QUERY_OPTS[DKEY]}
				if [[ ${ROW} -ne ${OMIT_NDX} ]];then
					((NDX++))
					_LOCAL_LIST[${NDX}]="${AGE}|${AUTHOR}|${TITLE}|${URL}|${TIME}|${AGETIME}"
					echo "${0}: ${NDX}\nAGE:${AGE}\nAUTHOR:${AUTHOR}\nTITLE:${TITLE}\nURL:${URL}\nTIME:${TIME}\nAGETIME:${AGETIME}\n-------------------------------------------" >>row_data
					OMIT_NDX=0
				fi
				AGE='' && AUTHOR='' && TIME='' && URL='' && TITLE='' && AGETIME='' 
			fi
			LAST_ROW=${ROW}
		fi
	done
}

query_rumble () {
	local GEN_SEARCH=''
	local QRY=''
	local QRY_TYPE=''

	QRY_TYPE=${_QUERY_MAP[${_QUERY_OPTS[EOPT]:l}]}
	[[ -z ${QRY_TYPE} ]] && msg_box -p -PK "${0}:Invalid query type:${_QUERY_OPTS[EOPT]}" && return 1
	_QUERY_OPTS[TYPE]=${QRY_TYPE}

	ping -w1 www.rumble.com >/dev/null
	[[ ${?} -ne 0 ]] && msg_box -p -PK "Rumble is down" && return 1

	[[ ${_DEBUG} -ge ${_LOW_DBG} ]] && dbg "${functrace[1]} called ${0}:${LINENO}: ARGC:${#@}"

	# Queries
	if [[ ${QRY_TYPE} == 'live' ]];then
		msg_box -c "Requesting <c><I>livestream channel<N> search..."
		QRY="https://rumble.com/c/${_QUERY_OPTS[QKEY]}/livestreams"
		_RUMBLE_PAGE=$(wget -q -O - ${QRY})
	elif [[ ${QRY_TYPE} == 'channel' ]];then
		msg_box -c "Requesting <c><I>channel<N> search..."
		QRY="https://rumble.com/c/${_QUERY_OPTS[QKEY]}?page=1"
		_RUMBLE_PAGE=$(wget -q -O - ${QRY})
	elif [[ ${QRY_TYPE} == 'general' ]];then
		msg_box -c "Requesting <c><I>general<N> search..."
		GEN_SEARCH=$(sed 's/ /%20/g' <<<${_QUERY_OPTS[QKEY]})
		QRY="https://rumble.com/search/all?q=${GEN_SEARCH}&sort=date&section=sort"
		_RUMBLE_PAGE=$(wget -q -O - ${QRY})
	elif [[ ${QRY_TYPE} == 'user' ]];then
		msg_box -c "Requesting <c><I>livestream user<N> search..."
		QRY="https://rumble.com/user/${_QUERY_OPTS[QKEY]}/livestreams"
		_RUMBLE_PAGE=$(wget -q -O - ${QRY})
	fi

	[[ -n ${GEN_SEARCH} ]] && GEN_SEARCH='g'
	parse_rumble ${GEN_SEARCH}
	[[ ${?} -ne 0 ]] && return 1 || return 0
}

#--Begin GetOpts--
local -a OPTIONS
local OPTION
local DESC

read -r -d '' DESC<<EOF
Test rumble queries
Query types: ${(kv)_QUERY_MAP}
EOF

parse_opts () {
	local OPTS=${@}
	local -a OPTSTR
	local LETTER_OPT
	local O

	for O in {1..${#OPTS}};do
		[[ ${OPTS[${O}]} =~ '[A-Za-z]' ]] && LETTER_OPT=${OPTS[${O}]}
		[[ ${O} -eq 1 && ${OPTS[${O}]} == ":" ]] && continue
		[[ ${O} -gt 1 && ${OPTS[${O}]} == ":" ]] && OPTSTR+=$(opt_type ${LETTER_OPT}) && continue
		OPTSTR+="-${OPTS[${O}]}"
	done
	echo ${OPTSTR}
}

opt_exists () {
	local OPT=${1}
	[[ $(( $OPTIONS[(Ie)${OPT}] )) -ne 0 ]] && return 0 || return 1
}

opt_type () {
	local OPT=${1}
	case ${OPT} in
		d) echo "<DIR>";;
		f) echo "<FILE>";;
		t) echo "<FILE_TYPE>";;
	esac
}

usage () {
	local -a PENDING_MSGS=(${@})
	local OPTS=$(parse_opts ${OPTSTR})

	[[ ${_SMCUP} == 'true' ]] && do_rmcup

	tabs 5
	(
	echo -e "\n${WHITE_FG}\nUsage${RESET}: ${_SCRIPT} ${WHITE_FG}[${RESET}${OPTS}${WHITE_FG}]${RESET} <SEARCHTERM> <QUERY_TYPE>\n"
	echo -e "${WHITE_FG}-H${RESET} help"
	echo -e "${WHITE_FG}-D${RESET} debug (repeats increase level)"
	echo -e "\n${WHITE_FG}DESC${RESET}:${DESC}"
	) >&2

	[[ -n ${PENDING_MSGS} ]] && PENDING_MSGS=$(err_msg_exit "${PENDING_MSGS}")
	exit_leave ${PENDING_MSGS}
}

OPTSTR=":HD"
OPTIND=0

while getopts ${OPTSTR} OPTION;do
	case $OPTION in
     H) usage;;
     D) dbg_set_level;;
     :) print -u2 "${_SCRIPT_TAG} ${BOLD}${RED_FG}Option error${RESET}: -${OPTARG} requires an argument"; usage;;
    \?) print -u2 "${_SCRIPT_TAG} ${BOLD}${RED_FG}Unknown option${RESET} - ${OPTARG}"; usage;;
	esac
	[[ ${OPTION} != 'D' ]] && OPTIONS+=${OPTION}
done
shift $((OPTIND -1))
#--End GetOpts--

# Execution
clear
[[ ${#} -ne 0 ]] && SEARCHTERM=${1} || exit_leave "Missing Argument:<SEARCHTERM>"
[[ ${#} -eq 2 ]] && QRY_TYPE=${2} || exit_leave "Missing Argument:<QRY_TYPE>"
[[ -e ./Rumble.qry ]] && /bin/rm -f ./Rumble.qry

_QUERY_OPTS[QKEY]=${SEARCHTERM}
_QUERY_OPTS[DKEY]=${SEARCHTERM}
_QUERY_OPTS[EOPT]=${QRY_TYPE}

echo "SEARCHTERM:${_QUERY_OPTS[QKEY]}, TYPE:${_QUERY_OPTS[EOPT]}"

query_rumble

#echo "\nTITLES ${#TITLES}"
#echo "------"
#NDX=0
#for L in ${(o)TITLES};do
#	echo ${L}
#done
#
#echo "\nLINKS ${#LINKS}"
#echo "------"
#NDX=0
#for L in ${(o)LINKS};do
#	echo ${L}
#done
#
#echo "\nAGES ${#AGES}"
#echo "------"
#NDX=0
#for L in ${(o)AGES};do
#	echo ${L}
#done
#
#echo "\nDURATIONS ${#DURATIONS}"
#echo "---------"
#NDX=0
#for L in ${(o)DURATIONS};do
#	echo ${L}
#done

echo "\n_LOCAL_LIST ${#_LOCAL_LIST}"
for L in ${_LOCAL_LIST};do
	echo ${L}
done

exit_leave
