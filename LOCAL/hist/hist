#!/usr/bin/zsh
#Inline ansi
BOLD="\033[1m"
ITALIC="\033[3m"
RESET="\033[m"
REVERSE="\033[7m"
STRIKE="\033[9m"
UNDER="\033[4m"

BLACK_BG="\033[40m"

BLUE_FG="\033[34m"
CYAN_FG="\033[36m"
GREEN_FG="\033[32m"
MAGENTA_FG="\033[35m"
RED_FG="\033[31m"
WHITE_FG="\033[37m"
YELLOW_FG="\033[33m"
WHITE_ON_GREY="\033[0m\033[0;1;37;100m"

# Imports
_LIB_DIR=${ZSH_LIB_DIR:=/usr/local/lib}
source ${_LIB_DIR}/LIB_INIT.zsh # Must be first
source ${_LIB_DIR}/UTILS.zsh
source ${_LIB_DIR}/SELECT.zsh
source ${_LIB_DIR}/LIB_DEPS.zsh # Must be last
 
# Constants
_HIST_FILE=~/.zsh_history
_LAST_ACTION=/tmp/hist_action

typeset -A _HIST_KEYS=()

# Vars
_ALL=false
_HIST_LINES=46
_SEARCHTERM=''
_DELETE=false
_HIST_KEY=''
_SCRIPT=${0:t}
_ACTION_MSG=''

# Functions
collect_hist () {
	local SEARCHTERM=${1}
	local DATE TM CMD
	local D U

	clear
	fc -R
	msg_box "<w>Collecting history...<N>"

	while read DATE TM CMD;do
		[[ ${CMD} =~ "${_SCRIPT}$" ]] && continue
		if [[ -n ${_SEARCHTERM} ]];then
			[[ ! ${CMD} =~ ${_SEARCHTERM} ]] && continue
		fi
		D="${DATE} ${TM}"
		U=$(date -d ${D} +%s)
		echo "${U}#${D}#${CMD}" 
	done < <(fc -nrlt'%Y-%m-%d  %H:%M:%S' 1 100000) | sort -t'#' -k3 -u | sort -t'#' -k1 -r | print_hist
}

print_hist () {
	local HDR=''
	local LINE=''
	local -a LIST=()
	local NDX=0
	local HK=''
	local UT=''
	local M1=''
	local M2=''

	while read LINE;do
		if [[ ${_ALL} == 'false' ]];then
			((NDX++))
			[[ ${NDX} -gt ${_HIST_LINES} ]] && break
		fi
		UT=$(cut -d# -f1 <<<${LINE})
		LINE=$(cut -d# -f2,3 <<<${LINE})
		HK=$(cut -d# -f1 <<<${LINE})
		_HIST_KEYS[${HK:s/ /_/}]=${UT} # Stash unix time
		LIST+=${LINE[1,100]}
	done

	msg_box_clear

	if [[ -n ${_SEARCHTERM} ]];then
		HDR="<c>History containing<N>: \"${_SEARCHTERM}\""
	elif [[ ${_ALL} == 'true' ]];then
		HDR="<c>All History<N> (<w>${#LIST} lines<N>)"
	else
		HDR="<c>Last ${_HIST_LINES} $(str_pluralize line ${_HIST_LINES}) of History<N>"
	fi

	[[ ${#LIST} -eq 1 ]] && msg_box -p -PK -H1 "${HDR}|$(cut -d# -f1 <<<${LIST[1]}) $(cut -d# -f2 <<<${LIST[1]})" && return

	sel_set_list ${LIST}

	sel_set_app_keys d

	if [[ -e ${_LAST_ACTION} ]];then
		read _ACTION_MSG < ${_LAST_ACTION}
		M1=$(cut -d# -f1 <<<${_ACTION_MSG})
		M2=$(cut -d# -f2- <<<${_ACTION_MSG})
		msg_box -p -PK -H1 "<m>History line was deleted<N>|${M1}|\"${M2}\""
		/bin/rm -f ${_LAST_ACTION}
	fi

	sel_list -S -C -O${MAGENTA_FG} -sn -d'#' -H${HDR} -F"Selected command will be copied to the clipboard. Use keypad for extended page navigation" -M"(<w>d<N>)elete"
	if [[ -n ${_SEL_VAL} ]];then
		case ${_SEL_KEY} in
			d) _DELETE=true;;
		esac
	fi
}

#--Begin GetOpts--
local -a OPTIONS
local OPTION
local DESC

read -r -d '' DESC<<EOF
Display command line history
EOF

parse_opts () {
	local OPTS=${@}
	local -a OPTSTR
	local LETTER_OPT
	local O

	for O in {1..${#OPTS}};do
		[[ ${OPTS[${O}]} =~ '[a-z]' ]] && LETTER_OPT=${OPTS[${O}]}
		[[ ${O} -eq 1 && ${OPTS[${O}]} == ":" ]] && continue
		[[ ${O} -gt 1 && ${OPTS[${O}]} == ":" ]] && OPTSTR+=$(opt_type ${LETTER_OPT}) && continue
		OPTSTR+="-${OPTS[${O}]}"
	done
	echo ${OPTSTR}
}

opt_exists () {
	local OPT=${1}
	[[ $(( $OPTIONS[(Ie)${OPT}] )) -ne 0 ]] && return 0 || return 1
}

opt_type () {
	local OPT=${1}
	case ${OPT} in
		n) echo "<LINES>";;
		f) echo "<FILE>";;
	esac
}

usage () {
	local -a PENDING_MSGS=(${@})
	local OPTS=$(parse_opts ${OPTSTR})

	[[ ${_SMCUP} == 'true' ]] && do_rmcup

	tabs 5
	(
	echo -e "\n${WHITE_FG}\nUsage${RESET}: ${_SCRIPT} ${WHITE_FG}[${RESET}${OPTS}${WHITE_FG}]${RESET} [SEARCHTERM]\n"
	echo -e "${WHITE_FG}-H${RESET} help"
	echo -e "${WHITE_FG}-D${RESET} debug (repeats increase level)"
	echo -e "${WHITE_FG}-a${RESET} all history lines"
	echo -e "${WHITE_FG}-n${RESET} history lines (default: ${_HIST_LINES})"
	echo -e "\n${WHITE_FG}DESC${RESET}:${DESC}"
	) >&2

	exit_leave $(msg_exit W ${PENDING_MSGS})
}

OPTSTR=":HDan:"
OPTIND=0

while getopts ${OPTSTR} OPTION;do
	case $OPTION in
     H) usage;;
     D) dbg_set_level;;
	  a) _ALL=true;;
	  n) _HIST_LINES=${OPTARG};;
     :) print -u2 "${RED_FG}${_SCRIPT}${RESET}: option: -${OPTARG} requires an argument"; usage;;
    \?) print -u2 "${RED_FG}${_SCRIPT}${RESET}: unknown option -${OPTARG}"; usage;;
	esac
	[[ ${OPTION} != 'D' ]] && OPTIONS+=${OPTION}
done
shift $((OPTIND -1))
#--End GetOpts--

# Execution
[[ ${_DEBUG} -eq 0 ]] && do_smcup

[[ -n ${1} ]] && _SEARCHTERM=${1}

if ! validate_is_integer ${_HIST_LINES};then
	exit_leave $(msg_exit E "Option <-n> invalid - integer required")
fi

collect_hist ${_SEARCHTERM}

if [[ ${_DELETE} == 'true' ]];then
	_DELETE=false
	HK=$(cut -d# -f1 <<<${_SEL_VAL})
	HK=${HK:s/ /_/}
	sed -in "/${_HIST_KEYS[${HK}]}/d" ${_HIST_FILE} # Key is epoch time
	echo "${_SEL_VAL}" > ${_LAST_ACTION}
	exec ${0} ${@}
else
	cut -d'#' -f2 <<<${_SEL_VAL} | tr -d \\n | xclip -i -selection clipboard
fi

exit_leave
