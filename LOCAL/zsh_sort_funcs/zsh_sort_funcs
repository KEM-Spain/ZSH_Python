#!/usr/bin/zsh
# Inline ansi
BOLD="\033[1m"
ITALIC="\033[3m"
RESET="\033[m"
REVERSE="\033[7m"
STRIKE="\033[9m"
UNDER="\033[4m"

BLACK_BG="\033[40m"

BLUE_FG="\033[34m"
CYAN_FG="\033[36m"
GREEN_FG="\033[32m"
MAGENTA_FG="\033[35m"
RED_FG="\033[31m"
WHITE_FG="\033[37m"
YELLOW_FG="\033[33m"

WHITE_ON_GREY="\033[0m\033[0;1;37;100m"

# Declarations
typeset -A _BLACKLIST=(usage 1 parse_opts 1 opt_type 1 opt_exists 1)
typeset -A SEEN

# Constants
_FUNC_REGEX='^.* () {'
_FUNC_TAG='^# Functions'
_LIB_FUNC_TAG='^# LIB Functions'

# Imports
[[ -z ${ZSH_LIB_DIR} ]] && _LIB_DIR=/usr/local/lib || _LIB_DIR=${ZSH_LIB_DIR}
source ${_LIB_DIR}/LIB_INIT.zsh
source ${_LIB_DIR}/UTILS.zsh
source ${_LIB_DIR}/MSG.zsh
source ${_LIB_DIR}/LIB_DEPS.zsh

# Functions
cleanup () {
	[[ ${REPLACE_SORTED} == 'true' ]] && rm -f ${FN}.sorted.functions || rm -f ${FN}.new
	rm -f ${FN}.normalized
	[[ -L ${FN} ]] && unlink ${FN}
	exit_leave
}

replace_functions () {
	local FN=${1}
	local L
	local -a LIST=()
	local TAG=''

	[[ ! -e ${FN}.sorted.functions ]] && echo "Unable to proceed.  No file containing sorted functions found." && cleanup

	grep -q ${_LIB_FUNC_TAG} ${FN}.normalized >/dev/null 2>&1
	[[ ${?} -eq 0 ]] && TAG=${_LIB_FUNC_TAG} ||  TAG=${_FUNC_TAG}

	# Prepend function section if needed
	# grep -q ${TAG} ${FN}.normalized >/dev/null 2>&1
	# [[ ${?} -ne 0 ]] && sed -i "0,/.*() {$/s/.*() {$/${TAG}\n&/" ${FN}.normalized

	LIST=($(func_list ${FN}.normalized))

	# Delete existing funcs
	for L in ${(o)LIST};do
		[[ ${_BLACKLIST[${L}]} -eq 1 ]] && continue
		func_delete ${L} ${FN}.normalized
	done

	# Insert sorted funcs
	sed -i "/${TAG}/r ${FN}.sorted.functions" ${FN}.normalized
}

sort_functions () {
	local FN=${1}
	local -a LIST
	local L
	local -R3 NDX=0
	local LONGEST=0

	[[ ! -e ${FN}.normalized ]] && echo "Unable to proceed.  No file containing normalized functions found." && cleanup
	[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} ${FN}.normalized was created..."

	grep -q ${_FUNC_REGEX} <${FN}.normalized
	[[ $? -ne 0 ]] && echo "No functions found in file: ${FN}\n" && cleanup

	LIST=($(func_list ${FN}.normalized))
	[[ ${_DEBUG} -gt 0 ]] && dbg "${0}:${LINENO} LIST:${LIST}"
	echo "${WHITE_FG}${#LIST}${RESET} functions identified"

	for L in ${LIST};do
		[[ ${#L} -gt ${LONGEST} ]] && LONGEST=${#L}
	done

	# Sort function alpha
	for L in ${(o)LIST};do
		[[ ${USE_BLACKLIST} == 'true' && ${_BLACKLIST[${L}]} -eq 1 ]] && continue
		((NDX++))
		if [[ ${LIST_FUNCS} == 'true' ]];then
			[[ ${NDX} -eq 1 ]] && echo "${WHITE_FG}Functions in ${FN}${RESET}"
			echo "${WHITE_FG}${NDX}${RESET}) ${L}"
		else
			tput el; printf "\rProcessing function ${WHITE_FG}%2d${RESET} of ${WHITE_FG}%2d${RESET} functions ${WHITE_FG}%%${BOLD}${GREEN_FG}%.2f${RESET} \t%*s\n" ${NDX} ${#LIST} $(( NDX * 100. / ${#LIST} )) ${LONGEST} ${L}
			func_print ${FN}.normalized ${L} >>${FN}.sorted.functions
		fi
	done

	[[ ${LIST_FUNCS} == 'false' ]] && echo "\n${WHITE_FG}${NDX}${RESET} ${FN} functions written\nto:${WHITE_FG}${FN}.sorted.functions${RESET}\nby function name in alphanumeric order"
	echo
}

#--Begin GetOpts--
RESET="\033[m"
RED_FG="\033[31m"
WHITE_FG="\033[37m"

SCRIPT=${0:t}

parse_opts () {
	local OPTS=${@}
	local -a OPTSTR
	local LETTER_OPT
	local O

	for O in {1..${#OPTS}};do
		[[ ${OPTS[${O}]} =~ '[a-z]' ]] && LETTER_OPT=${OPTS[${O}]}
		[[ ${O} -eq 1 && ${OPTS[${O}]} == ":" ]] && continue
		[[ ${O} -gt 1 && ${OPTS[${O}]} == ":" ]] && OPTSTR+=$(opt_type ${LETTER_OPT}) && continue
		OPTSTR+="-${OPTS[${O}]}"
	done
	echo ${OPTSTR}
}

opt_type () {
	local OPT=${1}
	case ${OPT} in
		b) echo "<BLACKISTED FUNCTION>";;
		d) echo "<DIR>";;
		f) echo "<FILE>";;
		t) echo "<FILE_TYPE>";;
	esac
}

read -r -d '' DESC<<EOF
Sort script functions
\n\tSorted script functions may be placed:
\n\t1) into a copy of the original as $(opt_type f).new (-r)
\t2) into the original file with functions now sorted (-R)
\t3) into a separate ${ITALIC}functions only${RESET} file: $(opt_type f).sorted.functions (${WHITE_FG}default${RESET})
EOF

usage () {
	local OPTS=$(parse_opts ${OPTSTR})
	local -a PENDING_MSGS=(${@})

	tabs 5

	[[ ${_SMCUP} == 'true' ]] && do_rmcup

	(
	echo -e "\n${WHITE_FG}Usage${RESET}: ${_SCRIPT} ${OPTS} $(opt_type f)\n"
	echo -e "${WHITE_FG}-H${RESET} help"
	echo -e "${WHITE_FG}-D${RESET} debug"
	echo -e "${WHITE_FG}-B${RESET} no blacklist"
	echo -e "${WHITE_FG}-b $(opt_type b)${RESET} add blacklisted function"
	echo -e "${WHITE_FG}-l${RESET} list the functions in script file:$(opt_type f)"
	echo -e "${WHITE_FG}-r${RESET} create a duplicate script file:$(opt_type f).new with sorted functions"
	echo -e "${WHITE_FG}-R${RESET} replace existing file:$(opt_type f) with sorted functions"
	echo -e "\n${WHITE_FG}DESC${RESET}:${DESC}"
	) >&2

	[[ -n ${PENDING_MSGS} ]] && PENDING_MSGS=$(err_msg_exit "${PENDING_MSGS}")
	exit_leave ${PENDING_MSGS}
}

OPTSTR=":HDRBb:lr"

REPLACE_SORTED=false
REPLACE_FILE=false
LIST_FUNCS=false
USE_BLACKLIST=true

OPTIND=0
while getopts ${OPTSTR} OPTION;do
	case ${OPTION} in
     H) usage;;
     D) dbg_set_level;;
     B) USE_BLACKLIST=false;;
	  b) _BLACKLIST[${OPTARG}]=1;;
	  l) LIST_FUNCS=true;;
     r) REPLACE_SORTED=true;;
     R) REPLACE_FILE=true;;
     :) print -u2 "${SCRIPT}: option: -${OPTARG} requires an argument"; usage;;
    \?) print -u2 "${SCRIPT}: ${BOLD}${RED_FG}Unknown option${RESET} -${OPTARG}"; usage;;
	esac
done
shift $(( OPTIND - 1 ))
#--End GetOpts--

# Execution
[[ ${#} -ne 0 ]] && FN=${1} || usage "Missing argument:$(opt_type f)"
[[ ! -e ${FN} ]] && usage "File:${FN} not found"
[[ ! -e ${PWD}/${FN:t} ]] && ln -s ${FN} ${PWD} # Link file

exit
FN=${PWD}/${FN:t}

[[ -e ${FN}.normalized ]] && rm -f ${FN}.normalized
[[ -e ${FN}.sorted.functions ]] && rm -f ${FN}.sorted.functions

if [[ ${USE_BLACKLIST} == 'true' ]];then
	echo "${WHITE_FG}Blacklisted Functions${RESET}:"
	for B in ${(k)_BLACKLIST};do
		echo "${CYAN_FG}${B}${RESET}"
	done
	echo
fi

func_normalize ${FN} # Normalize function declaration syntax
sort_functions ${FN} # Extract sorted functions

# Early exits
[[ ${LIST_FUNCS} == 'true' ]] && exit_leave # Option was invoked in sort_functions
[[ ! -e ${FN}.sorted.functions ]] && exit_leave $(err_msg_exit "sort_functions: failed to create and output. Exiting.")

# Processing
grep ${_FUNC_REGEX} < ${FN}.sorted.functions | sort | uniq -c | grep -v -P '\s+1\s+'
if [[ ${?} -ne 1 ]];then
	exit_leave $(err_msg_exit W "WARNING:Found duplicates")
fi

if [[ ${REPLACE_FILE} == 'true' || ${REPLACE_SORTED} == 'true' ]];then 
	replace_functions ${FN}
	[[ ! -e ${FN}.normalized ]] && exit_leave $(err_msg_exit "Option -r failed - unable to locate ${FN}.normalized")
	cat -s ${FN}.normalized | cat -s > ${FN}.new
	echo "Modified script with functions sorted in file:${FN}.new"
fi

if [[ ${REPLACE_FILE} == 'true' ]];then 
	[[ ! -e ${FN}.new ]] && exit_leave $(err_msg_exit "Option -R failed - unable to locate ${FN}.new")
	mv -f ${FN}.new ${FN}
	chmod 775 ${FN}
	[[ ! -e ${FN}.sort_functions ]] && rm -f ${FN}.sort_functions # Cleanup
	echo "Replaced original file: ${FN} with functions sorted"
fi

cleanup
exit_leave
